include "Primarystar_Time_Stamps.pfx"
create Primarystar_Time_Stamps(0)

import Primarystar_fallingstar

	#screen star transition
setf cometbrightness 1.2#1.6
setf transitionperiod 1
setf cameraoffset 0.501

setf cometscreentilt -20
setf cometsize 0.2

metaparticles cometscreenstars
	yaw 0
	pitch 0
	roll 0
	size 1.3
	alpha 0 1 1 1 0
	source -box (0,1.5,0) -offset (-10,0,0)
	emit -dir (1,0,0) -speed 30 10
	life 2
	effect Primarystar_fallingstar#fallingstardisplaced
	#force -worldGravity 10
	rate 90 -loop $transitionperiod 1
end

effect cometscreenstars -rigid
	metaparticles cometscreenstars
end

effect cometscreenstarstiled -rigid
	effect cometscreenstars -alphaScale $cometbrightness -sizeScale $cometsize -rotateZ $cometscreentilt
end

effect cometscreenstarscamera -cameraAttached
	effect cometscreenstarstiled -rotateXYZ 90 0 0 -offset (0,$cameraoffset,0)
end

effect cometscreenstarstiledopposite -rigid
	effect cometscreenstars -alphaScale $cometbrightness -sizeScale $cometsize -rotateZ 180-$cometscreentilt
end

effect cometscreenstarscameraopposite -cameraAttached
	effect cometscreenstarstiledopposite -rotateXYZ 90 0 0 -offset (0,$cameraoffset,0)
end

sequence screencomettimed -noOverlap
	wait $t2-$t0-$transitionperiod/2
	play cometscreenstarscamera $t3-$t2
	play cometscreenstarscameraopposite $t4-$t3
	play cometscreenstarscamera $t5-$t4
	play cometscreenstarscameraopposite $t6-$t5
	play cometscreenstarscamera $t7-$t6
	play cometscreenstarscameraopposite
end

effect screencomettimed
	sequence screencomettimed
end

effect Primarystar_ScreenTransitions -hardStop
	effect screencomettimed
end

export Primarystar_ScreenTransitions